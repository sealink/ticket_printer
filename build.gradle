plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id "maven-publish"
    id 'java-library'
    id 'net.nemerosa.versioning' version '3.0.0'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.kt3k.coveralls' version '2.12.2'
}
group = 'au.com.sealink'

sourceCompatibility = JavaVersion.VERSION_19
targetCompatibility = JavaVersion.VERSION_19
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jacocoTestReport {
    reports {
        xml.required.set(true) // coveralls plugin depends on xml format report
        html.required.set(true)
    }
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

versioning {
    branchEnv = ['TRAVIS_BRANCH', 'TRAVIS_TAG', 'TRAVIS_PULL_REQUEST_BRANCH', 'GIT_BRANCH']
}

if(versioning.info.tag == null) {
    version = versioning.info.display
}
else {
    version = versioning.info.tag
}

checkstyle {
    toolVersion = '8.1'
}

checkstyle {
    configProperties = [
            'allowStaticMemberImports': 'true'
    ]
}

repositories {
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: 'commons-codec', name: 'commons-codec', version:'1.15'
    testImplementation 'junit:junit:4.13.2'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task versionInfo {
    doLast {
        println "Version: ${project.version}"
    }
}

artifacts {
    archives sourcesJar
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "jmortlock"
            name "John Mortlock"
            email "john.mortlock@sealink.com.au"
        }
    }

    scm {
        url "https://github.com/sealink/ticket_printer.git"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId 'au.com.sealink'
            artifactId 'printing'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'SeaLink Print Library')
                root.appendNode('name', 'SeaLink Print Library')
                root.appendNode('url', 'https://github.com/sealink/ticket_printer')
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/sealink/ticket_printer"
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
